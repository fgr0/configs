# My personal Theme...

PROMPT_LAERADOR_CMD_MAX_EXEC_TIME=${PROMPT_LAERADOR_CMD_MAX_EXEC_TIME:-10}

function prompt_laerador_prompt_char() {
  case "$KEYMAP" {
    vicmd)
      print -n "%F{red}%B%#%b%f"
      ;;
    *)
      print -n "%f%#%f"
  }  
}

function prompt_laerador_check_time() {
  integer elapsed
  let "elapsed = $EPOCHSECONDS - ${_prompt_laerador_cmd_timestamp:-$EPOCHSECONDS}"
  unset _prompt_laerador_cmd_timestamp

  local human=" " 

  if (( elapsed > $PROMPT_LAERADOR_CMD_MAX_EXEC_TIME )) {
    # https://github.com/sindresorhus/pretty-time-zsh
    local days=$(( elapsed / 60 / 60 / 24 ))
    local hours=$(( elapsed / 60 / 60 % 24 ))
    local minutes=$(( elapsed / 60 % 60 ))
    local seconds=$(( elapsed % 60 ))

    (( days > 0 )) && human+="${days}d "
    (( hours > 0 )) && human+="${hours}h "
    (( minutes > 0 )) && human+="${minutes}m "
    human+="${seconds}s"
  }

  typeset -g _prompt_laerador_cmd_elapsed=$human
}

function prompt_laerador_precmd() {
  vcs_info
  prompt_laerador_check_time
  _prompt_laerador_pwd=$(spwd)
}

function prompt_laerador_preexec() {
  _prompt_laerador_cmd_timestamp=$EPOCHSECONDS
}

function prompt_laerador_setup() {
  # Load required modules
  zmodload zsh/datetime

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hooks
  add-zsh-hook precmd prompt_laerador_precmd
  add-zsh-hook preexec prompt_laerador_preexec

  # Configure vcs styling
  zstyle ':vcs_info:*' enable git svn hg
  zstyle ':vcs_info:*:*' check-for-changes true
  zstyle ':vcs_info:*:*' unstagedstr ' ?'
  zstyle ':vcs_info:*:*' stagedstr ' Â±'
  zstyle ':vcs_info:*:*' formats '%F{242}:%b%u%c'
  zstyle ':vcs_info:*:*' actionformats '%F{242}:%b:%F{yellow}%a%F{242}%u%c'
  
  # Prompt sections
  local root="%(!.%F{red}%n.%F{white}${SSH_TTY:+%n})%f"
  local host="%f%(!.@%m.${SSH_TTY:+@%m})%f"
  local pwd="%F{blue}"'${_prompt_laerador_pwd}'"%f"
  local vcs='${vcs_info_msg_0_}'"%f"

  local elaptime='${_prompt_laerador_cmd_elapsed}'
  local code="%(?..%F{red}%?)%f"

  PROMPT="${root}${host} ${pwd}${vcs} "'$(prompt_laerador_prompt_char)'" "
  RPROMPT="${code} ${elaptime}"
  SPROMPT='%fzsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

  # Set special options
  prompt_opts=(cr percent subst)
}

prompt_laerador_setup "$@"
